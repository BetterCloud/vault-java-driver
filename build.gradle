apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.bettercloud'
//noinspection GroovyUnusedAssignment
archivesBaseName = 'vault-java-driver'
version '1.1.0'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId groupId
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url nexusUrl
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

dependencies {
    testCompile('junit:junit:4.11')
    testCompile('org.mockito:mockito-core:1.10.19')
    testCompile('org.eclipse.jetty:jetty-server:9.3.7.v20160115')
    testCompile('org.testcontainers:testcontainers:1.1.3')
    testCompile('org.slf4j:slf4j-api:1.7.21')
    testRuntime('org.slf4j:slf4j-simple:1.7.21')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}



//
// Separate unit tests from integration tests.  See: `src/test-integration/README.md`
//

configurations {
    unitTestsCompile.extendsFrom testCompile
    unitTestsRuntime.extendsFrom testRuntime
    integrationTestsCompile.extendsFrom testCompile
    integrationTestsRuntime.extendsFrom testRuntime
}

sourceSets {
    unitTests {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/test/java')
    }
    integrationTests {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/test-integration/java')
    }
}

task unitTest(type: Test) {
    testClassesDir = sourceSets.unitTests.output.classesDir
    classpath = sourceSets.unitTests.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTests.output.classesDir
    classpath = sourceSets.integrationTests.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }
}

//
// Deploying releases to Maven Central.
//
// Requires a `gradle.properties` file in your `$HOME/.gradle` directory, with

// signing.keyId = <ID of the private key in your secure keyring used for signing JAR's>
// signing.password = <public key password>
// signing.secretKeyRingFile = <full path to your keyring file (i.e secring.gpg)>
//
// ossrhUsername = sonatype.org JIRA user
// ossrhPassword = sonatype.org JIRA password
//

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'java-vault-driver'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Zero-dependency Java client for HashiCorp\'s Vault'
                url 'https://github.com/BetterCloud/vault-java-driver'

                scm {
                    connection 'https://github.com/BetterCloud/vault-java-driver.git'
                    developerConnection 'https://github.com/BetterCloud/vault-java-driver.git'
                    url 'https://github.com/BetterCloud/vault-java-driver'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/BetterCloud/vault-java-driver/blob/master/README.md'
                    }
                }

                developers {
                    developer {
                        id 'steve-perkins-bc'
                        name 'Steve Perkins'
                        email 'steve.perkins@bettercloud.com'
                    }
                }
            }
        }
    }
}
